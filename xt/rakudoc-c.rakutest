#!/usr/bin/env raku

=begin overview

Verify any C<C<>> tags have no internal trailing whitespace.
To explicitly allow it, prepend the C<C<>> with a
C<Z<ignore-code-ws>> comment, needed in rare cases.

=end overview

use Test;
use lib $*PROGRAM.parent(2).child('lib');

use Test-Files;
use Pod::Convenience;

my @files = Test-Files.pods;

if @files {
    plan +@files;
} else {
    plan :skip-all<No rakudoc files specified>
}

sub is-valid-c($contents) {
    for @$contents -> $string {
        is $string, $string.trim, "C<$string> no extra whitespace.";
    }
}

sub walk-content($x) {
    my $trailing-ok = False;
    for $x.contents -> $contents {
        next unless $contents;
        for @$contents -> $item {
            if $item ~~ Pod::FormattingCode and $item.type eq 'Z' {
                if $item.contents.lc ~~ 'ignore-code-ws' {
                  # skip the validity check for the next C<>
                  $trailing-ok = True;
                }
            }
            if $item ~~ Pod::FormattingCode and $item.type eq 'C' {
                is-valid-c($item.contents) unless $trailing-ok;
                $trailing-ok = False;
            } elsif $item !~~ Str {
                walk-content($item);
            }
        }
    }
}

# Loop over all tested files
for @files -> $file {
    my @chunks = extract-pod($file).contents;

    subtest $file => {
        walk-content($_) for @chunks;
    }
}
